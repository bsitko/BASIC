1 REM X16 ROULETTE
4 REM N - # CHOSEN PLAYER, C: CONVERSION OF C$ TO #
5 REM B: PLAYERS BET, CO: COLOR ODDS, M: PLAYER MONEY
6 REM C1: ROULETTE WHEEL COLOR, C$: COLOR CHOSEN BY PLAYER
7 REM RMAX: HOLD PLAYER MAX, MAX: TOTAL MAXIMUM
10 M=1000:ODDS=36:WIN=0:MAX=1000:B=0:CO=1:RMAX=1000
20 IFPEEK($D9)<>80 THEN SYS$FF5F: SET SCREEN TO 80 CHARACTERS
30 POKE 646,225:PRINTCHR$(147):REM SET BKG BLUE, CLEAR SCREEN
40 PRINT"RX16 ROULETTE":FORI=1TO2:PRINTCHR$(13):NEXT
50 N1=0:C1=0:C=0:C$="":REM CLEAR OUT ANY VALUES
60 PRINT"EDO YOU WANT TO GUESS A (N)EUMBER OR (C)EOLOR?"
70 GET A$:IFA$=""THEN70
80 IFA$="N"THENGOSUB1000
90 IFA$="C"THENGOSUB2000
100 GOSUB 5000: REM GET YOUR BET
110 GOSUB 8000: REM SPIN WHEEL
120 N1=INT(ODDS*RND(0)+1)
130 C1=INT(2*RND(0)+1)
140 PRINT"ETHE WHEEL STOPPED ON:"
150 IFC1=1THENPRINT"£R"N1" REDE"
160 IFC1=2THENPRINT"R"N1" BLACKE"
200 REM COMPUTE WINS OR LOSSES
240 IFN1=NTHENPRINT"QYOU GOT THE NUMBER":WIN=ODDS*B:M=M+WIN:GOTO300
250 IFC1=CTHENPRINT"QYOU GOT THE COLOR":WIN=CO*B:M=M+WIN:GOTO300
260 PRINT"QYOU LOST. BETTER LUCK NEXT TIME!":GOSUB6000:GOTO300
300 REM TELL PLAYER WHETHER THEY WON OR LOST
320 IFM<=0THENGOSUB 7000
330 GOSUB 9000
340 PRINT"EQWHY NOT PRESS YOUR LUCK? RYE OR RNE?"
350 GETA$:IFA$="" THEN 350
360 IF A$="Y" THEN GOTO 50
370 IF A$="N" THEN 7050
380 GOTO 350
1000 REM GUESS BY NUMBER
1005 INPUT"EWHAT NUMBER (1-36)";N
1010 IFN<1 OR N>ODDS THEN GOTO1005
1020 RETURN
2000 PRINT"EWHAT COLOR: £R(R)EDE OR R(B)LACKE?"
2010 GETC$:IFC$=""THEN2010
2020 IFC$="B"THENC=2:PRINT"QEYOU CHOSE BLACKE":REM BLACK CHOSEN
2030 IFC$="R"THENC=1:PRINT"QEYOU CHOSE £REDE":REM RED CHOSEN
2040 IFC$<>"B" AND C$<>"R"THEN2010
2050 RETURN
5000 REM COLLECT YOUR BET
5010 IFM<RMAXTHEN RMAX=M:IFRMAX>MAXTHENRMAX=MAX
5020 GOSUB 9000:PRINT "MAX BET IS $"RMAX
5030 INPUT "EQWHAT IS YOUR BET";B
5040 IFB<0ORB>MTHENPRINT"YOU DON'T HAVE ENOUGH MONEY":GOTO5030
5050 RETURN
6000 REM YOU LOST THAT BET
6010 M=M-B:IF M<=0THEN 7000
6020 RETURN
7000 REM OUT OF MONEY
7010 PRINT "ERYOU ARE BANKRUPT!"
7020 PRINT "QTRY AGAIN? YE OR NE?"
7030 GETA$:IFA$=""THEN7030
7040 IFA$="Y" THEN GOTO 10
7050 PRINT "ETHANKS FOR PLAYING!"
7060 IFM>0 THEN PRINT"YOU ARE LEAVING WITH $";M
7070 END
8000 PRINT"EQQSPINNING:Q":FORZ=1TO500STEP50 : REM SPIN THE ROULETTE WHEEL
8010 PRINT"╭●│":FORI=0TO500+Z:NEXT:PRINT"╮●│":FORI=0TO500+Z:NEXT
8020 PRINT"╯●│":FORI=0TO500+Z:NEXT:PRINT"╰●│":FORI=0TO500+Z:NEXT
8030 NEXTZ
8040 RETURN
9000 REM DISPLAY HOW MUCH MONEY YOU HAVE REMAINING
9010 PRINT"QERYOU HAVE $"M"REMAINING."
9020 RETURN
